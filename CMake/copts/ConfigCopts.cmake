# See absl/copts/copts.py and absl/copts/generate_copts.py
include(GENERATED_Copts)

set(BEANQUICK_LSAN_LINKOPTS "")
set(BEANQUICK_HAVE_LSAN OFF)
set(BEANQUICK_DEFAULT_LINKOPTS "")

if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
  set(BEANQUICK_DEFAULT_COPTS "${BEANQUICK_GCC_FLAGS}")
  set(BEANQUICK_TEST_COPTS "${BEANQUICK_GCC_FLAGS};${BEANQUICK_GCC_TEST_FLAGS}")
  set(BEANQUICK_RANDOM_RANDEN_COPTS "${BEANQUICK_RANDOM_HWAES_X64_FLAGS}")
elseif("${CMAKE_CXX_COMPILER_ID}" MATCHES "Clang")
  # MATCHES so we get both Clang and AppleClang
  if(MSVC)
    # clang-cl is half MSVC, half LLVM
    set(BEANQUICK_DEFAULT_COPTS "${BEANQUICK_CLANG_CL_FLAGS}")
    set(BEANQUICK_TEST_COPTS "${BEANQUICK_CLANG_CL_FLAGS};${BEANQUICK_CLANG_CL_TEST_FLAGS}")
    set(BEANQUICK_DEFAULT_LINKOPTS "${BEANQUICK_MSVC_LINKOPTS}")
    set(BEANQUICK_RANDOM_RANDEN_COPTS "${BEANQUICK_RANDOM_HWAES_MSVC_X64_FLAGS}")
  else()
    set(BEANQUICK_DEFAULT_COPTS "${BEANQUICK_LLVM_FLAGS}")
    set(BEANQUICK_TEST_COPTS "${BEANQUICK_LLVM_FLAGS};${BEANQUICK_LLVM_TEST_FLAGS}")
    set(BEANQUICK_RANDOM_RANDEN_COPTS "${BEANQUICK_RANDOM_HWAES_X64_FLAGS}")
    if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
      # AppleClang doesn't have lsan
      # https://developer.apple.com/documentation/code_diagnostics
      if(NOT CMAKE_CXX_COMPILER_VERSION VERSION_LESS 3.5)
        set(BEANQUICK_LSAN_LINKOPTS "-fsanitize=leak")
        set(BEANQUICK_HAVE_LSAN ON)
      endif()
    endif()
  endif()
elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")
  set(BEANQUICK_DEFAULT_COPTS "${BEANQUICK_MSVC_FLAGS}")
  set(BEANQUICK_TEST_COPTS "${BEANQUICK_MSVC_FLAGS};${BEANQUICK_MSVC_TEST_FLAGS}")
  set(BEANQUICK_DEFAULT_LINKOPTS "${BEANQUICK_MSVC_LINKOPTS}")
  set(BEANQUICK_RANDOM_RANDEN_COPTS "${BEANQUICK_RANDOM_HWAES_MSVC_X64_FLAGS}")
else()
  message(WARNING "Unknown compiler: ${CMAKE_CXX_COMPILER}.  Building with no default flags")
  set(BEANQUICK_DEFAULT_COPTS "")
  set(BEANQUICK_TEST_COPTS "")
  set(BEANQUICK_RANDOM_RANDEN_COPTS "")
endif()

if("${CMAKE_CXX_STANDARD}" EQUAL 98)
  message(FATAL_ERROR "Abseil requires at least C++11")
elseif(NOT "${CMAKE_CXX_STANDARD}")
  message(STATUS "No CMAKE_CXX_STANDARD set, assuming 11")
  set(BEANQUICK_CXX_STANDARD 11)
else()
  set(BEANQUICK_CXX_STANDARD "${CMAKE_CXX_STANDARD}")
endif()
